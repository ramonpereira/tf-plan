//////////////////////////////////////
//
//Author: Ramon Fraga Pereira (Adapted from Buser)
//
//////////////////////////////////////

domain Navigation_Problem{

	requirements = { 
		reward-deterministic 
	};

	types {
		dim: object;
	};
	
	pvariables {
	
		// Constant
		MINMAZEBOUND(dim): { non-fluent, real, default = -10.0 };
		MAXMAZEBOUND(dim): { non-fluent, real, default = 10.0 };

		MINACTIONBOUND(dim): { non-fluent, real, default = -1.0 };
		MAXACTIONBOUND(dim): { non-fluent, real, default = 1.0 };

		GOAL(dim): { non-fluent, real, default = 1.0 };

		PENALTY: {non-fluent, real, default = 1000000.0 };
		CENTER(dim): {non-fluent, real, default = 0.0};
		
		// Interm		
		distance: {interm-fluent,real,level=1 };
		scalefactor: {interm-fluent,real,level=2 };
		proposedLoc(dim):{interm-fluent, real, level=3};
 
		//State
		location(dim): {state-fluent, real, default = 0.0 };
				
		//Action
		move(dim): { action-fluent, real, default = 0.0 };
	};
	
	cpfs {

		distance = sqrt[sum_{?l:dim}[pow[(location(?l)-CENTER(?l)),2]]];
		scalefactor = 2.0/(1.0+exp[-2*distance])-0.99;
		proposedLoc(?l) = location(?l) + move(?l)*scalefactor;
		location'(?l)= if(proposedLoc(?l)<=MAXMAZEBOUND(?l) ^ proposedLoc(?l)>=MINMAZEBOUND(?l)) then proposedLoc(?l) 
					else (if(proposedLoc(?l)>MAXMAZEBOUND(?l)) then MAXMAZEBOUND(?l) else MINMAZEBOUND(?l));
	};
	
	action-preconditions {
		forall_{?l:dim} move(?l)<=MAXACTIONBOUND(?l);
		forall_{?l:dim} move(?l)>=MINACTIONBOUND(?l);
		forall_{?l:dim} location(?l)<=MAXMAZEBOUND(?l);
		forall_{?l:dim} location(?l)>=MINMAZEBOUND(?l);
	};

	reward = - sum_{?l: dim}[abs[GOAL(?l) - location(?l)]];
}

non-fluents Navigation_non {
	domain = Navigation_Problem;
	objects{
		dim: {x,y,z};
	};
	non-fluents {
		GOAL(x) = 0.5;
        GOAL(y) = 3.1;
        GOAL(z) = 4.6;
	};
}

instance is1 {
	domain = Navigation_Problem;
	non-fluents = Navigation_non;
	init-state{
		location(x) = 3.4;
		location(y) = 0.7;
		location(z) = -2.3;
	};
	max-nondef-actions = 3;
	horizon = 20;
	discount = 1.0;
}